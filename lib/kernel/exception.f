\
\  exception.f
\
\  Copyright (C) 1999-2003 Illya Kysil
\

CR .( Loading EXCEPTION definitions )

CREATE-REPORT @
CREATE-REPORT OFF

DEFER (.EXCEPTION) (S exc-id -- )

:NONAME
  ." Exception 0x" H.8
; IS (.EXCEPTION)

: .THROW-WORD
  THROW-WORD @ ?DUP IF CR 9 EMIT ." at '" COUNT TYPE ." '" 0 THROW-WORD ! THEN ;

: .THROW-ADDRESS
  THROW-ADDRESS @ ?DUP IF ."  (0x" H.8 ." )" 0 THROW-ADDRESS ! THEN ;

: (.EXC-ADDR)
  .THROW-WORD .THROW-ADDRESS
;

: .EXCEPTION (S exc-id -- )
  (.EXCEPTION) (.EXC-ADDR)
;

: (DO-EXCEPTION)  (S exc-id-test exc-id c-addr count -- exc-id-test c-addr count flag )
  2>R OVER = 2R> ROT
;

: (."EXCEPTION") (S exc-id-test c-addr count -- )
  ." IKForth exception (0x"
  2 PICK H.8 ." ): "
;

: (EXCEPTION) (S c-addr count exc-id -- )
  :NONAME
  >R
  POSTPONE LITERAL
  POSTPONE SLITERAL
  POSTPONE (DO-EXCEPTION)
  POSTPONE IF
  POSTPONE (."EXCEPTION")
  POSTPONE TYPE
  POSTPONE DROP
  POSTPONE ELSE
  POSTPONE 2DROP
  DEFER@ (.EXCEPTION)
  POSTPONE LITERAL
  POSTPONE EXECUTE
  POSTPONE THEN
  POSTPONE ;
  R>
  IS (.EXCEPTION) ;

: (DO-EXCEPTION-XT) (S exc-id-test exc-id xt -- exc-id-test xt flag )
  >R OVER = R> SWAP ;

: (EXCEPTION-XT) (S xt exc-id -- )
  :NONAME
  >R
  POSTPONE LITERAL
  POSTPONE LITERAL
  POSTPONE (DO-EXCEPTION-XT) 
  POSTPONE IF
  POSTPONE EXECUTE
  POSTPONE DROP
  POSTPONE ELSE
  POSTPONE DROP
  DEFER@ (.EXCEPTION)
  POSTPONE LITERAL
  POSTPONE EXECUTE
  POSTPONE THEN
  POSTPONE ;
  R>
  IS (.EXCEPTION) ;

VARIABLE FREE-EXCEPTION-ID
-256 FREE-EXCEPTION-ID !

: (GET-EXC-ID) (S -- exc-id )
  -1 FREE-EXCEPTION-ID DUP @ >R +! R> ;

: EXCEPTION (S c-addr count -- exc-id )
  (GET-EXC-ID) DUP >R (EXCEPTION) R> ;

: EXCEPTION-XT (S xt -- exc-id )
  (GET-EXC-ID) DUP >R (EXCEPTION-XT) R> ;

CREATE-REPORT !
