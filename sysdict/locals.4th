\
\  locals.4th
\
\  Unlicense since 1999 by Illya Kysil
\

REQUIRES" sysdict/locals-stack.4th"

CR .( Loading LOCALS definitions )

REPORT-NEW-NAME @
REPORT-NEW-NAME OFF

<ENV
   16    CONSTANT #LOCALS
   TRUE  CONSTANT LOCALS
   TRUE  CONSTANT LOCALS-EXT
ENV>

10240 CONSTANT /LOCALS-DATA-AREA

VARIABLE LOCALS-DP0
VARIABLE LOCALS-DP

VARIABLE CURRENT-#LOCALS
WORDLIST CONSTANT LOCALS-WORDLIST

VARIABLE PATCH-#LOCALS

VARIABLE #LOCAL-RT

: RESET-LOCALS
   LOCALS-DP0 @ DUP
   /LOCALS-DATA-AREA ERASE
   LOCALS-DP !
   0 LOCALS-WORDLIST WL>LATEST !
   0 CURRENT-#LOCALS !
   0 PATCH-#LOCALS !
   -1 #LOCAL-RT !
;

: INIT-LOCALS
   0 LOCALS-DP0 !
   /LOCALS-DATA-AREA ALLOCATE THROW LOCALS-DP0 !
   RESET-LOCALS
;
' INIT-LOCALS DUP STARTUP-CHAIN CHAIN.ADD EXECUTE

: DONE-LOCALS
   LOCALS-DP0 @ FREE THROW
;
' DONE-LOCALS SHUTDOWN-CHAIN CHAIN.ADD

: LOCALS-FRAME,
   PATCH-#LOCALS @ 0= IF
      ['] LIT COMPILE,
      HERE PATCH-#LOCALS !
      0 ,
      POSTPONE LOCALS-FRAME
   THEN
;

: LOCALS-UNFRAME,
   PATCH-#LOCALS @ IF
      POSTPONE LOCALS-UNFRAME
   THEN
;

: DOES>-(L@),
   DOES>
   DROP
   #LOCAL-RT @
   POSTPONE LITERAL
   POSTPONE (L@)
   -1 #LOCAL-RT !
;

: (L!),  (S value-xt -- ) \ runtime semantics
   DROP
   #LOCAL-RT @
   POSTPONE LITERAL
   POSTPONE (L!)
   -1 #LOCAL-RT !
;

: (L+!), (S value-xt -- ) \ runtime semantics
   DROP
   #LOCAL-RT @
   POSTPONE LITERAL
   POSTPONE (L+!)
   -1 #LOCAL-RT !
;

CREATE LOCAL-VT
   ' (L!), , ' (L+!), ,

CREATE LOCAL-XT IMMEDIATE
   LOCAL-VT ,
DOES>-(L@),

: SEARCH-LOCALS \ (S c-addr u -- 0 | xt 1 | xt -1 )
   2DUP
   LOCALS-WORDLIST SEARCH-WORDLIST
   ?DUP IF
      2SWAP 2DROP
      SWAP >BODY @ #LOCAL-RT !
      ['] LOCAL-XT SWAP
      EXIT
   THEN
   \ not found S: c-addr u
   DEFERRED SEARCH-NAME
;

' SEARCH-LOCALS IS SEARCH-NAME

: LOCAL, \ ( c-addr u -- )
   2>R (DO-CREATE) 2R> &IMMEDIATE HEADER, \ xt
   DROP
   CURRENT-#LOCALS @ ,
;

: LOCAL-INIT,
   CURRENT-#LOCALS @ POSTPONE LITERAL POSTPONE (L!)
;

: (LOCAL) \ ( c-addr u -- )
   DUP 0= IF
      \ last message
      2DROP
   ELSE
      LOCALS-FRAME,
      DP @ >R GET-CURRENT >R
      LOCALS-DP @ DP !
      LOCALS-WORDLIST SET-CURRENT
      USE-LOCATE >R
      FALSE USE-LOCATE !
      ['] LOCAL, CATCH
      R> USE-LOCATE !
      DP @ LOCALS-DP !
      R> SET-CURRENT R> DP !
      THROW
      LOCAL-INIT,
      1 CURRENT-#LOCALS +!
   THEN
   PATCH-#LOCALS @ ?DUP IF
      CURRENT-#LOCALS @ SWAP !
   THEN
; COMPILE-ONLY

: LOCALS| ( "name...name |" -- )
   BEGIN
      PARSE-NAME OVER C@
      [CHAR] | - OVER 1 - OR
   WHILE
      (LOCAL)
   REPEAT 2DROP 0 0 (LOCAL)
; IMMEDIATE

HERE CONSTANT UNDEFINED-VALUE

: MATCH-OR-END? ( c-addr1 u1 c-addr2 u2 -- f )
   2 PICK 0= >R COMPARE 0= R> OR
;

: SCAN-ARGS
   \ 0 c-addr1 u1 -- c-addr1 u1 ... c-addrn un n c-addrn+1 un+1
   BEGIN
      2DUP S" |"  MATCH-OR-END? 0= WHILE
      2DUP S" --" MATCH-OR-END? 0= WHILE
      2DUP S" :}" MATCH-OR-END? 0= WHILE
      ROT 1+ PARSE-NAME
   AGAIN THEN THEN THEN
;

: SCAN-LOCALS
   \ n c-addr1 u1 -- c-addr1 u1 ... c-addrn un n c-addrn+1 un+1
   2DUP S" |" COMPARE 0= 0= IF
      EXIT
   THEN
   2DROP PARSE-NAME
   BEGIN
      2DUP S" --" MATCH-OR-END? 0= WHILE
      2DUP S" :}" MATCH-OR-END? 0= WHILE
      ROT 1+ PARSE-NAME
      POSTPONE UNDEFINED-VALUE
   AGAIN THEN THEN
;

: SCAN-END ( c-addr1 u1 -- c-addr2 u2 )
   BEGIN
      2DUP S" :}" MATCH-OR-END? 0= WHILE
      2DROP PARSE-NAME
   REPEAT
;

: DEFINE-LOCALS ( c-addr1 u1 ... c-addrn un n -- )
   0 ?DO
      (LOCAL)
   LOOP
   0 0 (LOCAL)
;

: {: ( -- )
   0 PARSE-NAME
   SCAN-ARGS SCAN-LOCALS SCAN-END
   2DROP DEFINE-LOCALS
; IMMEDIATE/COMPILE-ONLY

: ;
   LOCALS-UNFRAME, RESET-LOCALS
   POSTPONE ;
; IMMEDIATE

: ;]
   LOCALS-UNFRAME,
   \ NO RESET-LOCALS is needed since state is handled by
   \ SAVE-DEFINITION-STATE and RESTORE-DEFINITION-STATE below
   POSTPONE ;]
; IMMEDIATE

: EXIT
   LOCALS-UNFRAME,
   \ NO RESET-LOCALS is needed since state is handled by ;
   POSTPONE EXIT
; IMMEDIATE/COMPILE-ONLY

: DOES>
   LOCALS-UNFRAME, RESET-LOCALS
   POSTPONE DOES>
; IMMEDIATE/COMPILE-ONLY

: ;CODE
   LOCALS-UNFRAME, RESET-LOCALS
   POSTPONE ;CODE
; IMMEDIATE

: :NONAME
   :NONAME RESET-LOCALS
;

: :
   : RESET-LOCALS
;

:NONAME
   DEFERRED SAVE-DEFINITION-STATE
   LOCALS-DP @
   LOCALS-WORDLIST WL>LATEST @
   CURRENT-#LOCALS @
   PATCH-#LOCALS @
   0 LOCALS-WORDLIST WL>LATEST !
   0 CURRENT-#LOCALS !
   0 PATCH-#LOCALS !
; IS SAVE-DEFINITION-STATE

:NONAME
   PATCH-#LOCALS !
   CURRENT-#LOCALS !
   LOCALS-WORDLIST WL>LATEST !
   LOCALS-DP !
   DEFERRED RESTORE-DEFINITION-STATE
; IS RESTORE-DEFINITION-STATE

REPORT-NEW-NAME !
