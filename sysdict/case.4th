\
\  case.4th
\
\  Unlicense since 1999 by Illya Kysil
\

CR .( Loading CASE definitions )

REPORT-NEW-NAME @
REPORT-NEW-NAME OFF

\ -----------------------------------------------------------------------------
\ CASE
\ -----------------------------------------------------------------------------

VARIABLE OF-PAIRS
VARIABLE ENDCASE-PAIRS

: CASE
   0
; IMMEDIATE/COMPILE-ONLY

: (ENDCASE)
   DROP
;

: ENDCASE
   POSTPONE (ENDCASE)
   BEGIN
      ?DUP
   WHILE
      ENDCASE-PAIRS ?PAIRS >RESOLVE
   REPEAT
; IMMEDIATE/COMPILE-ONLY

: (ENDOF)
   R> @ >R
;

: ENDOF
   OF-PAIRS ?PAIRS POSTPONE (ENDOF) >MARK SWAP >RESOLVE ENDCASE-PAIRS
; IMMEDIATE/COMPILE-ONLY

: (OF)
   OVER = IF DROP R> CELL+ ELSE R> @ THEN >R
;

: OF
   POSTPONE (OF) >MARK OF-PAIRS
; IMMEDIATE/COMPILE-ONLY

: (<OF)
   OVER > IF DROP R> CELL+ ELSE R> @ THEN >R
;

: <OF
   POSTPONE (<OF) >MARK OF-PAIRS
; IMMEDIATE/COMPILE-ONLY

: (>OF)
   OVER < IF DROP R> CELL+ ELSE R> @ THEN >R
;

: >OF
   POSTPONE (>OF) >MARK OF-PAIRS
; IMMEDIATE/COMPILE-ONLY

: (<OF<)
   1+ 2 PICK ROT ROT WITHIN IF DROP R> CELL+ ELSE R> @ THEN >R
;

: <OF<
   POSTPONE (<OF<) >MARK OF-PAIRS
; IMMEDIATE/COMPILE-ONLY

REPORT-NEW-NAME !
