import os

Import('env', 'fkernelPath', 'bootdict')
senv = env.Clone()

senv['productname'] = 'ikforth-dev-x86'
senv['buildername'] = 'product-builder'

fkernelExec = senv.execname('${buildername}')

senv.InstallAs('${buildername}.img', bootdict)
senv.InstallAs(fkernelExec, fkernelPath)

sysdict_deps = senv.SConscript('#sysdict/SConstruct-config', exports = ['env'])

product_src = []
product_src.extend(sysdict_deps)
product_src.extend(senv.Glob('#lib/*.4th'))
product_src.extend(senv.Glob('#lib/~ik/*.4th'))
product_src.extend(senv.Glob('#lib/~js/486asm/*.F'))
product_src.extend(senv.Glob('#lib/~js/486asm/*.4th'))

product_deps = []
product_deps.extend(senv.Glob('*.4th'))
product_deps.extend(senv.Glob('#product/ikforth-base-x86/*.4th'))
product_deps.extend(product_src)

productdict = senv.Command('${productname}.img',
        fkernelExec,
        'IKFORTHTERMINIT=${TERMINIT} VERSION_STRING=${GIT_DESC} ${BUILD_LAUNCHER} ${SOURCE} --dict-size 2M -- ${TARGET}')
senv.Depends('${productname}.img', ['${buildername}.img', fkernelExec, product_deps])

Return('productdict')
